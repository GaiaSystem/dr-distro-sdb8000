#@TYPE: Machine
#@NAME: SDB8000
#@SOC: i.MX8MM
#@DESCRIPTION: Data Respons sdb8000 board

IMX_DEFAULT_BSP = "nxp"

MACHINEOVERRIDES =. "mx8:mx8m:mx8mm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

MACHINE_FEATURES += "pci wifi bluetooth screen"

# OP-TEE is also applicable to NXP BSP, mainline BSP seems not to have
# a full support for it yet.
MACHINE_FEATURES_append_use-nxp-bsp = " optee"

SERIAL_CONSOLES = "115200;ttymxc1"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "kernel-modules kernel-module-vmcu linux-firmware-sd8997"
MACHINE_EXTRA_RRECOMMENDS += "udev-rules"

KERNEL_DEVICETREE = "freescale/sdb8000.dtb freescale/sdb8000-xt70610a.dtb freescale/sdb8000-m101gwwf.dtb"
KERNEL_CLASSES += "kernel-fitimage"
KERNEL_IMAGETYPE_forcevariable = "fitImage"
PREFERRED_PROVIDER_virtual/kernel = "linux-imx"
# Build "Image" as well to enable generating fitimages from recipes
KERNEL_ALT_IMAGETYPE = "Image"
IMAGE_FSTYPES = "tar.bz2"
INITRAMFS_FSTYPES = "cpio.gz"
INITRAMFS_MAXSIZE = "512000"

UBOOT_LOADADDRESS_forcevariable = "0x40600000"
UBOOT_ENTRYPOINT_forcevariable = "0x40600000"
UBOOT_DTB_LOADADDRESS_forcevariable = "0x43000000"

# meta-freescale sets default packages to all images that we don't want
IMAGE_BOOT_FILES = ""

# We don't wan't kernel image included by default in images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

# Needed to build kernel
UBOOT_CONFIG ??= "sd"

NVRAM_INTERFACE = "mtd"

# Vivante only supports fdo backend
PREFERRED_PROVIDER_virtual/wpebackend = "wpebackend-fdo"
